#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WIDTH 1600
#define HEIGHT 900
#define WORKGROUP_SIZE 16
#define HEXBUFFER_WIDTH 779
#define HEXBUFFER_HEIGHT 900
layout (local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1) in;

layout(push_constant) uniform constants
{
    mat4 model;
    mat4 view;
    mat4 projection;
    float zoom;
    float time;
} PushConstants;

struct hex_node {
    vec3 color;
};

layout (binding = 0, rgba8) uniform image2D resultImage;
layout (std430, binding = 1) readonly buffer HexGrid {
    hex_node[] nodes;
} hexgrid;

vec3[6] updirs = vec3[6](
vec3(-1.0, 1.0, 0.0), // 0
vec3(-1.0, 0.0, 1.0), // 1
vec3(0.0, -1.0, 1.0), // 2
vec3(1.0, -1.0, 0.0), // 3
vec3(1.0, 0.0, -1.0), // 4
vec3(0.0, 1.0, -1.0)  // 5
);

vec3 screenPosToHexCoord(vec2 position, float diameter);
int calcHexIndex(vec3 position);

vec3 cosColor(float t, vec3 d, vec3 e, vec3 f, vec3 g)
{
    return vec3(d + e*cos(6.28318*(f*t+g)));
}

void main()
{
    // Terminate unnecessary workgroup threads
    if (gl_GlobalInvocationID.x >= WIDTH || gl_GlobalInvocationID.y >= HEIGHT)
    return;

    // Camera setup
    float x = float(gl_GlobalInvocationID.x) / float(WIDTH);
    float y = float(gl_GlobalInvocationID.y) / float(HEIGHT);

    vec2 uv = vec2(x, y) * 2.0 - 1.0;
    uv.x *= WIDTH / float(HEIGHT);
    uv += vec2(HEXBUFFER_WIDTH, HEXBUFFER_WIDTH) * PushConstants.zoom * PushConstants.zoom;

    // Calc hex position
    vec3 pos = screenPosToHexCoord(uv / 2.0f, PushConstants.zoom * PushConstants.zoom);
    int hexindex = calcHexIndex(pos);

    vec3 color = vec3(0);
    if (hexindex >= 0) {
        color = hexgrid.nodes[hexindex].color;
//        color = vec3(hexindex / 100.0);
    }

    // Color
//    vec3 d = vec3(0.3, 0.3, 0.5);
//    vec3 e = vec3(-0.2, -0.3, -0.5);
//    vec3 f = vec3(2.1, 2.0, 3.0);
//    vec3 g = vec3(0.0, 0.1, 0.0);
//    color = cosColor(color.r / 1.5 + .5, vec3(0.3, 0.3, 0.5), vec3(0.5, 0.3, 0.5), vec3(1.1, 0.4, 0.8), vec3(0.4, 0.1, 0.3));

    imageStore(resultImage, ivec2(gl_GlobalInvocationID.xy), vec4(color, 1));
//    vec2 ps = vec2(hexindex % WIDTH, floor(hexindex / float(WIDTH)));
//    if (hexindex < 0) ps = vec2(0);
//    imageStore(resultImage, ivec2(gl_GlobalInvocationID.xy), vec4(vec3(ps.xy / 100.0f, 0), 1));

}

int calcHexIndex(vec3 pos)
{
    int offset = int(ceil(pos.z / 2.0)) - 1;
    if (pos.y + offset > -2 || pos.x + offset >= HEXBUFFER_WIDTH || pos.z > HEXBUFFER_HEIGHT) return -1;
    return int(pos.x) + int(pos.z) * HEXBUFFER_WIDTH + offset;
}

vec3 screenPosToHexCoord(vec2 position, float diameter)
{
    mat2 hexToScreenMatrix = mat2(-1, 0, -1.0 / 2.0f, sqrt(3.0f) / 2.0);

    vec2 posInHexSpace = inverse(hexToScreenMatrix) * (position / vec2(diameter));

    vec3 newPos;
    newPos.x = -posInHexSpace.x - posInHexSpace.y;
    newPos.y = posInHexSpace.x;
    newPos.z = posInHexSpace.y;

    // How much does the position deviate from a unit coord?
    vec3 roundDelta;
    roundDelta.x = abs(round(newPos.x) - newPos.x);
    roundDelta.y = abs(round(newPos.y) - newPos.y);
    roundDelta.z = abs(round(newPos.z) - newPos.z);

    // Recalculate the axis with the biggest error
    vec3 nodepos;
    if (roundDelta.z > roundDelta.x && roundDelta.z > roundDelta.y)
    {
        // Z biggest error
        nodepos.x = round(newPos.x);
        nodepos.y = round(newPos.y);
        nodepos.z = -nodepos.x - nodepos.y;
    } else if (roundDelta.y > roundDelta.x && roundDelta.y > roundDelta.z)
    {
        // Y biggest error
        nodepos.x = round(newPos.x);
        nodepos.z = round(newPos.z);
        nodepos.y = -nodepos.x - nodepos.z;
    } else
    {
        // X biggest error
        nodepos.y = round(newPos.y);
        nodepos.z = round(newPos.z);
        nodepos.x = -nodepos.y - nodepos.z;
    }

    return nodepos;
}